plugins {
    id 'org.springframework.boot' version "2.4.5" apply false
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
}

allprojects {
    group "io.github.matrix"

    repositories {
        mavenLocal()
        maven { url "https://maven.aliyun.com/repository/public" }
        mavenCentral()
        maven { url "https://artifacts.alfresco.com/nexus/content/repositories/activiti-releases" }
    }

    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, "minutes"
        resolutionStrategy.cacheDynamicVersionsFor 0, "seconds"
    }
}

subprojects {

    apply from: "$rootDir/gradle/publications.gradle"

    plugins.withType(JavaPlugin) {

        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11

        publishing {
            publications {
                mavenJava(MavenPublication) {
                    version "$version"
                    from components.java
                    versionMapping {
                        usage('java-api') {
                            fromResolutionOf('runtimeClasspath')
                        }
                        usage('java-runtime') {
                            fromResolutionResult()
                        }
                    }
                }
            }
        }

        configurations {
            compileOnly {
                extendsFrom annotationProcessor
            }
        }

        test {
            useJUnitPlatform()
        }

    }

}