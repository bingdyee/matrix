plugins {
    id "com.jfrog.artifactory" version "4.16.1"
    id 'java-platform'
    id 'maven-publish'
}

group = "io.matrix"

ext{
    set('matrixVersion', "$version")
}

javaPlatform {
    allowDependencies()
}

dependencies {
    api platform("io.matrix:matrix-parent:$matrixVersion")
    constraints {
        api "io.matrix:matrix-spring-boot-starter:$matrixVersion"
        api "io.matrix:matrix-toolkit:$matrixVersion"
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = "matrix-dependencies"
            from components.javaPlatform
            pom {
                afterEvaluate {
                    name = "Matrix Project Dependencies"
                    description = "Matrix Project (Bill of Materials)"
                }
                url = "https://github.com/vbintx/matrix"
                organization {
                    name = "Mebiux"
                    url = "https://github.com/mebiux"
                }
                developers {
                    developer {
                        id = "vbintx"
                        name = "Noa Swartz"
                        email = "fetaxyu@gmail.com"
                    }
                }
                licenses {
                    license {
                        name = "Apache License, Version 2.0"
                        url = "https://www.apache.org/licenses/LICENSE-2.0"
                        distribution = "repo"
                    }
                }
                issueManagement {
                    system = "GitHub Issues"
                    url = "https://github.com/vbintx/matrix/issues"
                }
            }
        }
    }
    repositories {
        maven {
            credentials {
                username "$repoUsername"
                password "$repoPassword"
            }
            url = version.endsWith('SNAPSHOT') ? "$snapshotRepoUrl" : "$releaseRepoUrl"
        }
    }
}

artifactoryPublish {
    publications(publishing.publications.mavenJava)
}
